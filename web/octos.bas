20 GOTO1000
21 ' Location subroutine.
22 ' Input: HX,HY is human position; CX,CY is Octon position.
23 ' Output: MV is move; DS is distance
30 DX=HX-CX:DY=HY-CY
40 IFDX=0THENIFDY>0THENDS=DY:MV=2:RETURNELSEDS=-DY:MV=8:RETURN
50 IFDY=0THENIFDX>0THENDS=DX:MV=6:RETURNELSEDS=-DX:MV=4:RETURN
60 DS=ABS(DX):IFDS<ABS(DY)THENDS=ABS(DY)
70 IFDY>0THENIFDX>0THENMV=3:RETURNELSEMV=1:RETURN
80 IFDX>0THENMV=9:RETURNELSEMV=7:RETURN
81 ' Check for Octon removed; set flag if so
90 FG=0:IFC(I,4)<=0THENC(I,4)=0:PRINT@876+2*I," ";:PRINT@940+2*I," ";:FG=-1:RETURNELSERETURN
91 ' Check for ALL Octons removed; if so, human wins.
100 LF=0:FORK=1TONC:PRINT@468,F$(K+8);:IFC(K,0)THENLF=LF+1:NEXTKELSENEXTK
110 IFLF>0THENRETURNELSE4000
111 ' Move direction subroutine.
112 ' Input: MV is move; PX,PY is present location.
113 ' Output: NX,NY is new location
120 NX=PX:NY=PY:ONMVGOTO130,140,150,160,,170,180,190,200
130 NX=PX-1:NY=PY+1:RETURN
140 NY=PY+1:RETURN
150 NX=PX+1:NY=PY+1:RETURN
160 NX=PX-1:RETURN
170 NX=PX+1:RETURN
180 NX=PX-1:NY=PY-1:RETURN
190 NY=PY-1:RETURN
200 NX=PX+1:NY=PY-1:RETURN
201 ' Shield-raised subroutine.
210 PRINT@109,"SHIELD RAISED ";:IFHTTHENPRINT@875,"Y";:HD=-1:RETURNELSEPRINT@876+2*I,"Y";:C(I,5)=-1:RETURN
211 ' Shield-down subroutine.
220 PRINT@109,"SHIELD LOWERED";:IFHTTHENPRINT@875,"N";:HD=0:RETURNELSEPRINT@876+2*I,"N";:C(I,5)=0:RETURN
230 IFHTTHENMM=HLELSEMM=C(I,7)
231 ' Message subroutine.
240 PRINT@107+64*MM,MM;M$;:IFHTTHENRETURNELSEFORK=1TO3:FORJ=1TO25:PRINT@468,F$(J);:NEXTJ,K:RETURN
241 ' Change the strength display.
250 IFHTTHENPRINT@938,USING"##";HS;:RETURNELSEPRINT@939+2*I,C(I,4);:RETURN
251 ' Clear the message display
260 FORL=171TO747STEP64:PRINT@L,STRING$(21,32);:NEXTL:RETURN
270 PRINT@64*PY+3*PX,".";:PRINT@64*NY+3*NX,P$;:RETURN
271 ' Out-of-arena? subroutine.
272 ' Input: NX,NY is new (proposed) position.
273 ' Output: AN is out-of-arena flag.
280 AN=0:IFBD(NX,NY)=-2THENAN=-1:RETURNELSERETURN
281 ' Into pit? subroutine.
282 ' Input: NX,NY is new (proposed) position
283 ' Output: FG is pit flag.
290 FG=0:IFBD(NX,NY)=-1THENFG=-1:RETURNELSERETURN
291 ' Subroutine to check for stone.
292 ' Input: PX,PY is present location; NX,NY is new location
293 ' Output: FG is "passed stone" flag.
300 FG=0:IFBD(NX,NY)>2ANDBD(NX,NY)=BD(PX,PY)THENFG=-1:RETURNELSERETURN
301 ' In-the-pit subroutine.
302 ' Input: P$ is player; HT is human's-turn flag.
303 ' Output: NX,NY is new location.
304 ' Internal: J,K,FG
310 FG=0:RN=RND(8):ONRNGOTO320,330,340,350,360,370,380,390
320 NX=7:NY=5:GOTO400
330 NX=8:NY=6:GOTO400
340 NX=9:NY=7:GOTO400
350 NX=8:NY=8:GOTO400
360 NX=7:NY=9:GOTO400
370 NX=6:NY=8:GOTO400
380 NX=5:NY=7:GOTO400
390 NX=6:NY=6
400 IFHX=NXANDHY=NYTHEN310
410 FORK=1TO3:FORN=1TO25:PRINT@468,LEFT$(F$(N),1)+P$+RIGHT$(F$(N),1);:NEXTN,K
420 FORJ=1TONC:PRINT@468,LEFT$(F$(J),1)+P$+RIGHT$(F$(J),1);:IFC(J,0)=0THEN440
430 IFC(J,I)=NXANDC(J,2)=NYTHEN310
440 NEXTJ:IFHTTHEN460
450 C(I,1)=NX:C(I,2)=NY:C(I,4)=C(I,4)-1:GOSUB90:IFFGTHENC(I,0)=0:GOSUB100:RETURNELSEGOSUB220:GOSUB250:PRINT@469," ";:GOSUB270:RETURN
460 HX=NX:HY=NY:HS=HS-1:IFHS<=0THEN6000ELSEGOSUB220:GOSUB250:PRINT@469," ";:GOSUB270:RETURN
461 ' Program begins. Declaration and initalization. Display title page while
462 ' loading arrays.
1000 RANDOM:CLEAR150:DEFINTA-Z:DIMC(8,8),BD(14,14),F$(25)
1010 CLS:PRINTCHR$(23);:PRINT@398,"ARENA OF OCTOS":PRINT@718,"BY AL JOHNSTON":PRINT@782,"APRIL 25, 1981"
1020 FORY=0TO14:FORX=0TO14:BD(X,Y)=-2:NEXTX,Y
1030 FORY=1TO5:X1=6-Y:X2=8+Y:GOSUB1040:NEXTY:GOTO1050
1040 FORX=X1TOX2:BD(X,Y)=0:BD(X,14-Y)=0:NEXTX:RETURN
1050 FORY=6TO8:FORX=1TO13:BD(X,Y)=0:NEXTX,Y
1060 X=4:Y=3:GOSUB1070:X=9:GOSUB1070:Y=10:GOSUB1070:X=4:GOSUB1070:GOTO1080
1070 BD(X,Y)=1:BD(X+1,Y+1)=1:BD(X+1,Y)=3:BD(X,Y+1)=3:RETURN
1080 X=3:Y=5:GOSUB1090:X=10:GOSUB1090:Y=8:GOSUB1090:X=3:GOSUB1090:GOTO1100
1090 BD(X,Y)=5:BD(X+1,Y+1)=5:BD(X+1,Y)=4:BD(X,Y+1)=4:RETURN
1100 FORX=6TO8:BD(X,5)=2:BD(X,9)=2:NEXTX
1110 FORY=6TO8:FORX=5TO9:BD(X,Y)=2:NEXTX,Y
1120 BD(7,6)=-1:BD(6,7)=-1:BD(7,7)=-1:BD(8,7)=-1:BD(7,8)=-1
1130 FORI=1TO25:F$(I)=CHR$(128+RND(63))+CHR$(128+RND(63))+CHR$(128+RND(63)):NEXTI
1131 ' Ask for instructions, practice session
1140 CLS:PRINT@450,"DO YOU WANT INSTRUCTIONS?"
1150 A$=INKEY$:IFA$=""THEN1150ELSEIFA$="Y"THEN5000ELSEIFA$<>"N"THEN1150
1160 PRINT"DO YOU WANT A PRACTICE SESSION?"
1170 A$=INKEY$:IFA$=""THEN1170ELSEIFA$="Y"THENDD=0ELSEIFA$="N"THENDD=-1ELSE1170
1180 NC=0
1190 NC=NC+1:IFDD=0ANDNC>2THENDD=-1:NC=1
1191 ' Set up the Octon array.
1200 Y=0:I=0:GOSUB1220:Y=5:GOSUB1220:Y=7:GOSUB1220:Y=8:GOSUB1220
1210 Y=4:I=8:GOSUB1220:Y=6:GOSUB1220:Y=3:I=-3:GOSUB1220:GOTO1230
1220 FORX=1TO8:C(X,Y)=I:NEXTX:RETURN
1230 FORI=1TO8:READX,Y:C(I,1)=X:C(I,2)=Y:NEXTI
1240 DATA5,1,9,13,13,5,1,9,9,1,5,13,13,9,1,5
1250 FORI=1TONC:C(I,0)=-1:NEXTI:HT=0
1251 ' Display the arena and place the Octons
1260 CLS:FORX=26TO59:SET(X,2):SET(X,42):NEXT
1270 FORY=15TO29:SET(0,Y):SET(1,Y):SET(84,Y):SET(85,Y):NEXT
1280 Y=14:FORX=2TO24STEP2:SET(X,Y):SET(X+1,Y):SET(84-X,Y):SET(85-X,Y):Y=Y-1:NEXT
1290 Y=30:FORX=2TO24STEP2:SET(X,Y):SET(X+1,Y):SET(84-X,Y):SET(85-X,Y):Y=Y+1:NEXT
1300 READX,Y:IFX=0THEN1320ELSEFORI=XTOYSTEP3:PRINT@I,".";:NEXTI:GOTO1300
1310 DATA79,91,140,158,201,225,262,292,323,359,387,402,408,423,451,463,475,487,515,530,536,551,579,615,646,676,713,737,780,798,847,859,0,0
1320 FORX=41TO44:SET(X,19):SET(X,25):NEXT:FORX=39TO40:SET(X,20):SET(X+6,20):SET(X,24):SET(X+6,24):NEXT:FORY=21TO23:SET(37,Y):SET(38,Y):SET(47,Y):SET(48,Y):NEXT
1330 A$=CHR$(130)+CHR$(129):PRINT@269,A$;:PRINT@284,A$;:PRINT@394,A$;:PRINT@415,A$;:PRINT@586,A$;:PRINT@607,A$;:PRINT@717,A$;:PRINT@732,A$;
1340 A$=CHR$(160)+CHR$(144):PRINT@205,A$;:PRINT@220,A$;:PRINT@330,A$;:PRINT@351,A$;:PRINT@522,A$;:PRINT@543,A$;:PRINT@653,A$;:PRINT@668,A$;
1350 PRINT@834,"ARENA";:PRINT@897,"OF OCTOS";
1360 PRINT@811,"H  1 2 3 4 5 6 7 8";:PRINT@866,"SHIELD:";:PRINT@928,"STRENGTH:";
1370 FORI=1TONC:PRINT@(64*C(I,2)+3*C(I,1)-1),I;:PRINT@876+2*I,"N";:PRINT@940+2*I,"8";:NEXTI
1380 PRINT@875,"N";:PRINT@938,"10";
1381 ' Place the human at random about the firepit.
1390 HT=-1:PX=7:PY=4:P$="H":HS=11:GOSUB310:GOSUB270:HD=0:HM=10:HL=0:IFDDTHEN3000ELSEPRINT@98,"PRACTICE";:PRINT@164,"COMBAT";:GOTO3000
1391 ' Alternate HT between 0 and -1.
2000 HT=-(HT+1):IFHTTHEN3000
2001 ' Octon's turn. If Octon has been removed, go to the next one.
2010 FORI=1TONC:PRINT@468,F$(I);:IFC(I,0)=0THEN2620
2011 ' Define the player string. Display it and clear the message display.
2012 ' Number of moves starts at 0: human "chance of hit" set to -3.
2020 P$=RIGHT$(STR$(I),1):PRINT@46,"OCTON NUMBER ";P$;:GOSUB260:C(I,7)=0:HC=-3
2021 ' If Octon is unshielded, show it.
2030 IFC(I,5)THENGOSUB210ELSEGOSUB220
2031 ' Increment number of turns. If done, go to the next Octon.
2040 C(I,7)=C(I,7)+1:IFC(I,7)>C(I,6)THEN2620
2041 ' Find out which Octon is the weakest.
2050 WK=-1:FORL=1TONC:PRINT@468,F$(L);:IFC(L,0)=0THEN2060ELSEIFC(L,4)<C(I,4)THENWK=0:GOTO2060ELSE2060
2060 NEXTL
2061 ' Define CX and CY. Get direction and distance to human.
2070 CX=C(I,1):CY=C(I,2):GOSUB30
2071 ' CL is the number of turns left.
2080 CL=C(I,6)-C(I,7)+1
2081 ' Octon's logic. Depending on factors such as distance, number of moves
2082 ' remaining, which Octon is weakest, and human strength, the Octon will
2083 ' either move, raise shield, or strike.
2090 IFCL>2THEN2250
2100 IFCL>1THEN2140
2110 IFDS<>1THEN2340
2120 IFHS=1ORLF>1THEN2130ELSE2340
2130 IFWKTHEN2340ELSE2350
2140 IFDS<>1THEN2180
2150 IFLF>1THEN2170
2160 IFHS<=2THEN2350ELSE2380
2170 IFWKTHEN2340ELSE2350
2180 IFDS>2THEN2220
2190 IFLF>1THEN2210
2200 IFHS<=1THEN2370ELSE2380
2210 IFWKTHEN2380ELSE2370
2220 IFLF>1THEN2240
2230 IFC(I,4)>HS-6THEN2370ELSE2380
2240 IFWKTHEN2380ELSE2370
2250 IFDS<>1THEN2310
2260 IFLF>1THEN2280
2270 IFSBTHENSB=0:AN=-1:GOTO2380ELSE2350
2280 IFWKTHEN2300
2290 IFSBTHENSB=0:GOTO2360ELSE2350
2300 IFSBTHENSB=0:GOTO2380ELSE2350
2310 IFLF>1THEN2330
2320 GOTO2370
2330 IFWKTHEN2380ELSE2370
2331 ' Octon raises shield.
2340 GOSUB210:M$="RAISE SHIELD":GOSUB230:GOTO2040
2350 SB=-1:GOSUB2580:GOTO2040
2360 GOSUB2460
2370 PX=C(I,1):PY=C(I,2):GOSUB120:GOSUB2390:GOTO2040
2380 IFANTHENAN=0:GOSUB2460:GOTO2370ELSEMV=MV-2*(MV-5):RT=-1:GOTO2370
2381 ' Octon moves. Set message, depending on retreat flag.
2390 M$="ADVANCE":IFRTTHENM$="RETREAT":RT=0
2391 ' Check for "out of arena."
2400 GOSUB280:IFANTHENM$=M$+" & HIT WALL":GOSUB230:RETURN
2401 ' Check for "in pit"
2410 GOSUB290:IFFGTHENM$=M$+" INTO PIT":GOSUB230:PRINT@64*PY+3*PX,".";:GOSUB310:IFC(I,4)<>0THENRETURNELSEPRINT@64*PY+3*PX,".";:C(I,7)=8:RETURN
2411 ' Make sure that the Octon isn't moving into a space occupied by the human
2412 ' or another Octon.
2420 IFHX=NXANDHY=NYTHENGOSUB2460:GOTO2390
2430 FORJ=1TONC:PRINT@468,F$(J);:IFC(J,0)=0THEN2450
2440 IFC(J,1)=NXANDC(J,2)=NYTHENGOSUB2460:GOTO2390
2450 NEXTJ:GOTO2560
2451 ' If there is an obstruction, go around it.
2460 RN=RND(2):RN=RN-2:ONMVGOTO2470,2480,2490,2500,,2510,2520,2530,2540
2470 IFRNTHENMV=2:GOTO2550ELSEMV=4:GOTO2550
2480 IFRNTHENMV=1:GOTO2550ELSEMV=3:GOTO2550
2490 IFRNTHENMV=2:GOTO2550ELSEMV=6:GOTO2550
2500 IFRNTHENMV=1:GOTO2550ELSEMV=7:GOTO2550
2510 IFRNTHENMV=3:GOTO2550ELSEMV=9:GOTO2550
2520 IFRNTHENMV=4:GOTO2550ELSEMV=8:GOTO2550
2530 IFRNTHENMV=7:GOTO2550ELSEMV=9:GOTO2550
2540 IFRNTHENMV=6:GOTO2550ELSEMV=8:GOTO2550
2550 GOSUB120:RETURN
2551 ' If there are no obstructions, show the Octon and check for a stone.
2560 GOSUB270:C(I,1)=NX:C(I,2)=NY:GOSUB300:IFNOTFGTHENGOSUB230:RETURN
2561 ' Octon has passed a stone: There is a 50% chance of tripping and losing
2562 ' a turn.
2570 IFRND(2)=1THENGOSUB230:RETURNELSEM$=M$+" AND TRIP":GOSUB230:GOSUB220:C(I,7)=C(I,7)+1:IFC(I,7)>C(I,6)THENRETURNELSEM$="GETTING UP":GG=64*C(I,2)+3*C(I,1):PRINT@GG,CHR$(95);:GOSUB230:PRINT@GG,P$;:RETURN
2571 ' Octon swings. Set message and lower shield. If human is unshielded then
2572 ' hit; otherwise increase human's "chance of hit" variable.
2580 M$="SWING":GOSUB220:IFNOTHDTHEN2600ELSEHC=HC+4:IFHC>9THENHC=9
2581 ' Find out if Octon has hit human.
2590 IFRND(10)>HCTHENM$=M$+" - DEFLECTED":GOSUB230:RETURN
2591 ' Human hit; add to the message and print it. Decrement human strength, and
2592 ' show it.
2600 M$=M$+" AND HIT":GOSUB230:HS=HS-1:IFHS>0THENPRINT@938,HS;:RETURN
2601 ' The human has lost all strength points, and loses. Show the human
2602 ' removed, and go to the closing statements.
2610 PRINT@64*HY+3*HX,".";:PRINT@938,HS;:FORN=1TO4:FORJ=1TO25:PRINT@468,F$(J);:NEXTJ,N:GOTO5210
2611 ' Go on to the next Octon's turn. If it's the last one, switch HT and go to
2612 ' the human's turn.
2620 FORN=1TO5:FORJ=1TO25:PRINT@468,F$(J);:NEXTJ,N:GOSUB260:NEXTI:GOTO2000
2621 ' Human's turn. Show heading, clear the message display, define player
2622 ' string, and set number of turns left to 0
3000 PRINT@46,"HUMAN'S TURN  ";:GOSUB260:P$="H":HL=0
3001 ' If human is defended, show it.
3010 IFHDTHENGOSUB210ELSEGOSUB220
3011 ' Set all Octon "chance of hit" variables to -3.
3020 FORI=1TONC:PRINT@468,F$(I);:C(I,3)=-3:NEXTI
3021 ' Increment number of turns; if done, switch HT and let Octons take over.
3030 HL=HL+1:IFHL>HMTHEN2000
3031 ' Input routine. (This also keeps the fire going.)
3040 I=26
3050 I=I-1:IFI=0THEN3040
3060 A$=INKEY$:PRINT@468,F$(I);:IFA$=""THEN3050
3061 'If A$ is a number, or one of the alternate movement keys, go to the move
3062 ' subroutine.
3070 IFA$>="1"ANDA$<="9"THEN3190
3075 IFA$="U"THENA$="7":GOTO3190ELSEIFA$="I"THENA$="8":GOTO3190ELSEIFA$="O"THENA$="9":GOTO3190ELSEIFA$="J"THENA$="4":GOTO3190
3076 IFA$="L"THENA$="6":GOTO3190ELSEIFA$="M"THENA$="1":GOTO3190ELSEIFA$=","THENA$="2":GOTO3190ELSEIFA$="."THENA$="3":GOTO3190
3077 ' If A$ is not one of the other three valid keys, return.
3080 IFA$<>"S"ANDA$<>"R"ANDA$<>"P"THEN3050
3081 ' The human raises his shield.
3090 IFA$="R"THENM$="RAISE SHIELD":GOSUB230:GOSUB210:GOTO3030
3091 ' The human passes.
3100 IFA$="P"THENM$="PASS":GOSUB230:FORN=1TO4:FORJ=1TO25:PRINT@468,F$(J);:NEXTJ,N:GOTO2000
3101 ' The human swings. Check to see which Octon is next to him.
3110 M$="SWING":GOSUB220:FORI=1TONC:PRINT@468,F$(I+8);:IFC(I,0)=0THEN3140
3111 ' K and L will be 1 if Octon is nearby.
3120 K=ABS(C(I,1)-HX):L=ABS(C(I,2)-HY)
3121 ' Set Octon's "I'm hit" variable if so.
3130 IFK<=1ANDL<=1THENC(I,8)=-1ELSEC(I,8)=0
3131 ' Look again to see if any Octons were hit.
3140 NEXTI:FORI=1TONC:PRINT@468,F$(I);:IFC(I,0)=0ORC(I,8)=0THEN3180
3141 ' Increase Octon's chance of getting hit.
3150 C(I,3)=C(I,3)+4:IFC(I,3)>9THENC(I,3)=9
3151 ' Octon will be hit if undefended, or if the random number comes up. If he
3152 ' loses all his strength points, remove him.
3160 IFC(I,5)=0ORRND(10)<C(I,3)THENM$=M$+" AND HIT":GOSUB230:C(I,4)=C(I,4)-1:HT=0:GOSUB250:HT=-1:GOSUB90:IFFGTHENC(I,0)=0:GOSUB100:PRINT@64*C(I,2)+3*C(I,1),".";:GOTO3030ELSE3030
3161 ' The strike is deflected; show it and return
3170 M$=M$+" - DEFLECTED":GOSUB230:GOTO3030
3171 ' The human has swung his sword and there was no one there.
3180 NEXTI:M$=M$+" AND MISSED":GOSUB230:GOTO3030
3181 ' The human moves. If A$ was a 5, go back and try again.
3190 IFA$="5"THEN3040ELSEM$="MOVE"
3191 ' Define the variable MV. Go set the new X and Y positions, and check for
3192 ' "out of arena."
3200 MV=VAL(A$):PX=HX:PY=HY:GOSUB120:GOSUB280
3210 IFANTHENM$=M$+" AND HIT WALL":GOSUB230:GOTO3030
3211 ' Loop to see if there is an Octon in the way. If so, you are pushing him;
3212 ' if not, next Octon.
3220 FORI=1TONC:PRINT@468,F$(I);:IFC(I,0)=0THEN3240
3230 IFC(I,1)=NXANDC(I,2)=NYTHEN3280
3240 NEXTI
3241 ' Check for firepit; if yes, go to the pit subroutine.
3250 IFBD(NX,NY)=-1THENM$=M$+" INTO PIT!":GOSUB230:PRINT@64*PY+3*PX,".";:GOSUB310:GOTO3030
3251 ' Place the human and check to see if there was a stone. If not, or if so
3252 ' and luck is in your favor, then no problem. Otherwise lose the turn.
3260 GOSUB270:HX=NX:HY=NY:GOSUB300:IFNOTFGOR(FGANDRND(2)>1)THENGOSUB230:GOTO3030
3270 M$=M$+" AND TRIPS":GOSUB220:GOSUB230:HL=HL+1:IFHL>HMTHEN200ELSEGG=64*HY+3*HX:PRINT@GG,CHR$(95);:M$="GETTING UP":GOSUB230:FORK=1TO3:FORM=1TO25:PRINT@468,F$(M);:NEXTM,K:PRINT@GG,"H";:GOTO3030
3271 ' Human pushes Octon. Save human's present location, then set the location
3272 ' to which the Octon is being pushed
3280 X1=NX:Y1=NY:PX=NX:PY=NY:GOSUB120
3281 ' Define the message and check to see if the Octon's pushed position is out
3282 ' of the arena. If so, Octon's chance of hit increases.
3290 M$="PUSH":GOSUB280:IFANTHENC(I,3)=C(I,3)+4:IFC(I,3)>9THENC(I,3)=9
3291 ' Also, show the message and go to the next turn.
3300 IFANTHENM$=M$+" OCTON TO WALL":GOSUB230:GOTO3030
3301 ' Check to see if Octon's pushed position is already occupied.
3310 FORJ=1TONC:PRINT@468,F$(J);:IFC(J,0)=0THEN3330
3320 IFC(J,1)=NXANDC(J,2)=NY:THENC(I,3)=C(I,3)+4:GOSUB230:IFC(I,3)>9THENC(I,3)=9:GOTO3030ELSE3030
3321 ' Move the human to his new position.
3330 NEXTJ:PRINT@64*HY+3*HX,".";:PRINT@64*Y1+3*X1,"H";:HX=X1:HY=Y1
3331 ' Check to see if Octon has been pushed into firepit; place him in his
3332 ' position.
3340 IFBD(NX,NY)=-1THENM$=M$+" INTO FIREPIT":GOSUB230:HT=0:P$=RIGHT$(STR$(I),1):GOSUB310:PRINT@64*Y1+3*X1,"H";:HT=-1:P$="H":IFHDTHENGOSUB210:GOTO3030ELSEGOTO3030
3350 PRINT@(64*NY+3*NX),RIGHT$(STR$(I),1);:C(I,1)=NX:C(I,2)=NY:GOSUB300
3351 ' Check to see if Octon has been pushed across stone.
3360 IFFGTHENM$=M$+" ACROSS STONE":GOSUB230:GOTO3030ELSEGOSUB230:GOSUB3030
3361 ' Human has won.
4000 G$="":IFNC>1THENG$="S"
4010 CLS:PRINT@325,"CONGRATULATIONS! YOU HAVE SUCCEEDED IN FIGHTING"NC"OCTON WARRIOR"G$".":PRINT
4020 IFDDTHEN4080ELSEIFNC=2THEN4060
4030 PRINT"YOU HAVE COMPLETED THE FIRST HALF OF TRAINING."
4040 PRINT"TO CONTINUE TRAINING, PRESS <ENTER>";
4050 INPUTA:RESTORE:GOTO1190
4060 PRINT"THIS IS THE END OF YOUR TRAINING. YOU ARE NOW READY TO START THE CONTEST."
4070 PRINT"PRESS <ENTER> TO BEGIN";:INPUTA:RESTORE:GOTO1190
4080 IFNC=8THEN4110ELSEG$="ST":IFNC=2THENG$="ND"ELSEIFNC=3THENG$="RD"ELSEIFNC>3THENG$="TH"
4090 PRINT"YOU HAVE COMPLETED THE"STR$(NC);G$" CONTEST IN THE OCTON CHALLENGE. YOU MAY REST IF YOU WISH, THEN PRESS ANY KEY TO CONTINUE."
4100 A$=INKEY$:IFA$=""THEN4100ELSENC=NC+1:RESTORE:GOTO1200
4110 CLS:PRINTCHR$(23):PRINT@320,"YOU HAVE WON THE OCTON CHALLENGE"
4120 PRINT"BUT REMEMBER... IF YOU SHOW ANY SIGN OF COWARDICE (EVEN IF YOU TYPE 'RUN') YOU WILL BE CAPTURED ONCE AGAIN...":PRINT:END
4130 GOTO4130
4131 ' Instructions.
5000 CLS:PRINT@20,"ARENA OF OCTOS"
5010 PRINT"YOUR STAR CRUISER HAS BEEN CAPTURED WHILE TRAVELING THROUGH SPACE CLAIMED BY THE OCTON DYNASTY.  AFTER SOME DELIBERATION, THE COUNCIL OF EIGHT HAS GRANTED YOU THE CHANCE TO EARN YOUR FREEDOM BY ACCEPTING THE 'OCTON CHALLENGE'."
5020 PRINT"YOU WILL BE PLACED IN AN ARENA, WITH A SWORD AND A SHIELD.  YOU WILL THEN ENGAGE ONE OCTON IN COMBAT.  IF YOU SURVIVE, YOU WILL THEN FIGHT TWO, THEN THREE.... UNTIL THERE ARE EIGHT OCTONS IN THE ARENA."
5030 PRINT"THE OCTONS HAVE EIGHT MOVES, AND A 'STRENGTH' FACTOR OF EIGHT.  THIS MEANS THAT AN OCTON CAN RECEIVE EIGHT DIRECT BLOWS BEFORE HE IS REMOVED FROM THE ARENA."
5040 PRINT@976,"PRESS <ENTER> TO CONTINUE";
5050 A$=INKEY$:IFA$=""THEN5050
5060 CLS:PRINT"THE ARENA OF OCTOS IS AN EIGHT-SIDED STRUCTURE.  THERE ARE EIGHT STONES PLACED IN THE ARENA, TO TRIP THE CONTESTANTS. IN THE CENTER OF THE ARENA IS ANOTHER HAZARD, THE FIRE PIT."
5070 PRINT"THE COUNCIL OF EIGHT HAS DETERMINED THAT HUMANS ARE TO BE ALLOWED TEN MOVES, AND TEN STRENGTH POINTS SINCE HUMANS ARE DECIMAL CREATURES."
5080 PRINT"IF YOU CROSS A STONE DURING THE CONTEST, YOU HAVE A 50% CHANCE OF TRIPPING AND LOSING A TURN.  IF YOU ENTER THE FIREPIT, YOU WILL LOSE ONE STRENGTH POINT, AND YOU WILL LOWER YOUR SHIELD AND BLINDLY ESCAPE IN ANY DIRECTION."
5090 PRINT@976,"PRESS <ENTER> TO CONTINUE";
5100 A$=INKEY$:IFA$=""THEN5100
5110 CLS:PRINT@47,"7";:PRINT@55,"8";:PRINT@63,"9";:PRINT@175,"4";:PRINT@191,"6";:PRINT@303,"1";:PRINT@311,"2";:PRINT@319,"3";
5120 FORX=98TO123:SET(X,7):NEXT:FORY=3TO10:SET(110,Y):SET(111,Y):NEXT:Y=2:FORX=98TO106STEP2:SET(X,Y):SET(X+1,Y):SET(X+16,Y+6):SET(X+17,Y+6):Y=Y+1:NEXT
5130 Y=12:FORX=98TO106STEP2:SET(X,Y):SET(X+1,Y):SET(X+16,Y-6):SET(X+17,Y-6):Y=Y-1:NEXT
5140 PRINT@0,"      YOU MOVE IN THE ARENA BY";:PRINT@64,"PRESSING KEYS 1-9 OR THE LETTER KEYS";:PRINT@128,"INDICATED BY THE CHART, TO MOVE IN";
5150 PRINT@192,"ONE OF THE EIGHT DIFFERENT";:PRINT@256,"DIRECTIONS.";
5160 PRINT@384,"        YOUR ONLY OTHER COMMANDS ARE: <S> SWING YOUR SWORD <R> RAISE YOUR SHIELD <P> PASS (AND FORFEIT REMAINING MOVES)"
5170 PRINT"IF AN OCTON HAS HIS SHIELD RAISED, YOUR FIRST SWING HAS A 10% CHANCE OF CONNECTING.  THE SECOND SWING HAS A 50% CHANCE, AND AFTER THAT THERE IS A 90% CHANCE OF A HIT."
5180 PRINT@976,"PRESS <ENTER> TO CONTINUE";
5190 A$=INKEY$:IFA$=""THEN5190
5200 CLS:PRINT@256,"THE COUNCIL OF EIGHT HAS GRANTED YOU A 'PRACTICE' SESSION, TO FAMILIARIZE YOU WITH THE ARENA.":PRINT:GOTO1160
5201 ' Human has lost
6000 G$="":IFNC>1THENG$="S"
6010 CLS:PRINT@325,"YOU HAVE LOST IN YOUR BATTLE AGAINST"NC"OCTON WARRIOR"G$".":IFDDTHENPRINT"YOU WILL NOW REMAIN AN OCTON SLAVE, AND BE FORCED TO WORK AT THE OCTAL PALACE FOR SCRAPS OF FOOD":PRINT"YOU WILL HAVE TO TRY AGAIN WHEN YOU THINK YOU ARE  ":END
6020 PRINT"SINCE THIS IS A PRACTICE SESSION, YOU ARE ALLOWED TO TRY AGAIN."
6030 PRINT"PRESS <ENTER> TO CONTINUE PRACTICE";:INPUTA:RESTORE:GOTO1200
